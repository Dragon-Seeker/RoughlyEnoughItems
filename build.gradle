buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url "https://dist.creeper.host/Sponge/maven" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

plugins {
    id("maven-publish")
    id("java")
    id("java-library")
    id("net.minecrell.licenser") version("0.4.1")
    id("com.matthewprenger.cursegradle") version("1.4.0")
    id("net.corda.plugins.jar-filter") version("4.0.48") apply false
    id("com.github.johnrengelman.shadow") version("4.0.4") apply false
}

import java.text.SimpleDateFormat

archivesBaseName = "RoughlyEnoughItems"
version = project.mod_version
group = "me.shedaniel"

sourceSets {
    main {
        java {
            srcDir 'RoughlyEnoughItems-api/src/main/java'
            srcDir 'RoughlyEnoughItems-default-plugin/src/main/java'
            srcDir 'RoughlyEnoughItems-runtime/src/main/java'
        }
        resources {
            srcDir 'RoughlyEnoughItems-runtime/src/main/resources'
        }
    }
}

allprojects {
    apply plugin: 'net.minecraftforge.gradle'
    apply plugin: 'maven-publish'
    apply plugin: 'maven'
    apply plugin: 'net.minecrell.licenser'
    apply plugin: 'net.corda.plugins.jar-filter'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = targetCompatibility = 1.8

    configurations {
        shadow
    }

    repositories {
        maven { url "https://dl.bintray.com/shedaniel/autoconfig1u" }
        mavenLocal()
    }

    afterEvaluate {
        processResources {
            filesMatching('fabric.mod.json') {
                expand 'version': project.version
            }
            inputs.property "version", project.version
        }

        license {
            header rootProject.file('HEADER')
            include '**/*.java'
        }

        jar {
            from rootProject.file("LICENSE")
        }
    }

    minecraft {
        mappings channel: 'official', version: rootProject.minecraft_version
        accessTransformer = rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

        runs {
            client {
                workingDirectory project.file('run')

                mods {
                    create(project.name) {
                        source sourceSets.main
                    }
                }
            }

            server {
                workingDirectory project.file('run')

                mods {
                    create(project.name) {
                        source sourceSets.main
                    }
                }
            }
        }
    }

    dependencies {
        minecraft("net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}")
        compile(fg.deobf("me.shedaniel.cloth:cloth-config-forge:${cloth_config_version}"))
        runtimeOnly(fg.deobf("me.shedaniel:autoconfig1u-forge:${autoconfig1u}")) {
            transitive = false
        }
        compileOnly("me.shedaniel:autoconfig1u-forge:${autoconfig1u}") {
            transitive = false
        }
        shadow("me.shedaniel:autoconfig1u-forge:${autoconfig1u}") {
            transitive = false
        }
        compile("org.jetbrains:annotations:19.0.0")
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier("sources")
        from sourceSets.main.allSource
    }
}

subprojects {
    group = rootProject.group
    version = rootProject.version

    configurations {
        dev
        remapped
    }

    jar {
        classifier("dev")
    }

//    remapJar {
//        input.set(file("${project.buildDir}/filtered-libs/$archivesBaseName-${version}-dev-filtered.jar"))
//        classifier(null)
//    }
//
//    artifacts {
//        dev file: file("${project.buildDir}/libs/$archivesBaseName-${version}-dev.jar"), type: "jar", builtBy: jar
//        remapped file: file("${project.buildDir}/libs/$archivesBaseName-${version}.jar"), type: "jar", builtBy: remapJar
//    }

    task jarFilter(type: net.corda.gradle.jarfilter.JarFilterTask) {
        jars build
        annotations {
            forRemove = [
                    "org.jetbrains.annotations.NotNull",
                    "org.jetbrains.annotations.Nullable",
                    "org.jetbrains.annotations.ApiStatus\$Experimental",
                    "org.jetbrains.annotations.ApiStatus\$Internal",
                    "org.jetbrains.annotations.ApiStatus\$ScheduledForRemoval",
                    "org.jetbrains.annotations.ApiStatus\$AvailableSince",
                    "org.jetbrains.annotations.ApiStatus\$NonExtendable",
                    "org.jetbrains.annotations.ApiStatus\$OverrideOnly"
            ]
        }
    }

//    remapJar.dependsOn("jarFilter")

    publishing {
        publications {
            create("${archivesBaseName}_mavenJava", MavenPublication) {
                afterEvaluate {
//                    artifact(file("${project.buildDir}/libs/$archivesBaseName-${version}-maven.jar")) {
//                        builtBy remapMavenJar
//                    }
//                    artifact(sourcesJar) {
//                        builtBy remapSourcesJar
//                    }
                }
            }
        }

        repositories {
            if (project.hasProperty('danielshe_pass')) {
                maven {
                    url = "http://deploy.modmuss50.me/"
                    credentials {
                        username = "danielshe"
                        password = project.getProperty('danielshe_pass')
                    }
                }
            }
        }
    }
}

task licenseFormatAll
subprojects { licenseFormatAll.dependsOn("${path}:licenseFormat") }
/*subprojects { rootProject.remapJar.dependsOn("${path}:remapJar") }

task remapMavenJar(type: RemapJarTask, dependsOn: jar) {
    afterEvaluate {
        input.set(file("${project.buildDir}/libs/${archivesBaseName}-${version}-dev.jar"))
        archiveName = "${archivesBaseName}-${version}-maven.jar"
        addNestedDependencies.set(false)
    }
}*/

dependencies {
    afterEvaluate {
        subprojects.each {
            compile project(path: ":${it.name}")
//            include project(path: ":${it.name}", configuration: "remapped")
        }
    }
}

def releaseChangelog = "No changelog"

/* Thank you modmenu & fablabs */
task releaseOnCf {
    def df = new SimpleDateFormat("yyyy-MM-dd HH:mm")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    def branch
    if (System.env.BRANCH_NAME) {
        branch = System.env.BRANCH_NAME
        branch = branch.substring(branch.lastIndexOf("/") + 1)
    } else {
        branch = "git rev-parse --abbrev-ref HEAD".execute().in.text.trim()
    }
    if (branch == "HEAD") {
        branch = "git rev-parse --short HEAD".execute().in.text.trim()
    }
    def time = df.format(new Date())
    def changes = new StringBuilder()
    changes << "<h2>REI v$project.version for $project.supported_version</h2>Updated at <b>$time</b>.<br><a href=\"https://www.github.com/shedaniel/RoughlyEnoughItems/commits/$branch\">Click here for changelog</a>"
    def proc = "git log --max-count=200 --pretty=format:%s".execute()
    proc.in.eachLine { line ->
        def processedLine = line.toString()
        if (!processedLine.contains("New translations") && !processedLine.contains("Merge") && !processedLine.contains("branch")) {
            changes << "<br>- ${processedLine.capitalize()}"
        }
    }
    proc.waitFor()
    releaseChangelog = changes.toString()
    dependsOn tasks.getByName("curseforge")
}

curseforge {
    if (project.hasProperty('danielshe_curse_api_key') || System.getenv('danielshe_curse_api_key') != null) {
        apiKey = project.hasProperty('danielshe_curse_api_key') ? project.property('danielshe_curse_api_key') : System.getenv('danielshe_curse_api_key')
        project {
            id = "310111"
            releaseType = "release"
            changelogType = "html"
            changelog = releaseChangelog
            addGameVersion "1.16-Snapshot"
            addGameVersion "1.16.2"
            addGameVersion "1.16.3"
            addGameVersion "Java 8"
            addGameVersion "Forge"
            relations {
                requiredDependency "fabric-api"
                embeddedLibrary "cloth-api"
                embeddedLibrary "cloth-config"
                embeddedLibrary "auto-config-updated-api"
            }
            mainArtifact(file("${project.buildDir}/libs/${project.archivesBaseName}-${project.version}.jar")) {
                displayName = "[Forge $project.supported_version] v$project.version"
            }
            addArtifact(file("${project.buildDir}/libs/${project.archivesBaseName}-${project.version}-sources.jar")) {
                displayName = "[Forge $project.supported_version] v$project.version Sources"
            }
            afterEvaluate {
                uploadTask.dependsOn("build")
            }
        }
    }
    options {
        forgeGradleIntegration = false
        javaVersionAutoDetect = false
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
//            artifact(file("${project.buildDir}/libs/$archivesBaseName-${version}-maven.jar")) {
//                builtBy remapMavenJar
//            }
//            artifact(sourcesJar) {
//                builtBy remapSourcesJar
//            }
            pom.withXml {
                def depsNode = asNode().appendNode("dependencies")
                subprojects.each {
                    def depNode = depsNode.appendNode("dependency")
                    depNode.appendNode("groupId", it.group)
                    depNode.appendNode("artifactId", it.name)
                    depNode.appendNode("version", it.version)
                    depNode.appendNode("scope", "compile")
                }
            }
        }
    }
}
