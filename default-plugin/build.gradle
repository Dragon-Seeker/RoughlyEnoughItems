archivesBaseName = rootProject.name + "-" + project.name

loom {
    accessWidener = gradle.rootProject.project("fabric").file("src/main/resources/roughlyenoughitems.accessWidener")
}

dependencies {
    modCompileOnly("net.fabricmc:fabric-loader:${project.fabricloader_version}")
    modCompileOnly("me.shedaniel.cloth:cloth-config:${cloth_config_version}")
    modCompileOnly("me.shedaniel:architectury:${architectury_version}")
    compileOnly(project(path: ":api"))
}

architectury {
    common(forgeEnabled.toBoolean())
}

remapJar {
    afterEvaluate {
        remapAccessWidener.set(false)
    }
}

java {
    withSourcesJar()
}

task fakeJar(type: Jar, dependsOn: remapJar) {
    from remapJar.archiveFile.map { zipTree(it) }
    from(rootProject.file("fake/fabric.mod.json")) {
        into ""
    }
    archiveClassifier.set "fake"
}

publishing {
    publications {
        mavenCommon(MavenPublication) {
            artifactId = rootProject.name + "-" + project.name
            artifact(fakeJar) { classifier null }
            afterEvaluate {
                artifact(remapSourcesJar.output) {
                    builtBy remapSourcesJar
                    classifier "sources"
                }
            }
        }
    }
}
